# ====================================================================================================================================================================================
# Shop System
# スキルアップ編
# ====================================================================================================================================================================================

# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# 販売価格・販売量 ここで変えると全て一括で変更されます
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
on skript load:

    set {price.levelup.to2} to 64
    set {price.levelup.to3} to 64

# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
#
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
on right click on wandering trader:
    cancel event
    if {mode.skill} is true:
        if {skill.%player%} is "tyuuni":
            message "&b[商人] &r悪いがお前さんのレベルはあげられないな..." to player
            stop
        play sound "block.stone_button.click_on" to player
        open chest inventory with 3 row named "&2スキルアップ" to player
        set slot 11 of player's current inventory to wooden pickaxe named "&lレベル1"
        set slot 13 of player's current inventory to iron pickaxe named "&lレベル2"
        set slot 15 of player's current inventory to diamond pickaxe named "&lレベル3"
    else:
        broadcast "&b[商人] &r悪いが今は休業中なんだ..."

on inventory click:
    if event-item is wooden pickaxe named "&lレベル1":
        cancel event
        if {level.%player%} is 1:
            play sound "block.note_block.banjo" to player
            message "&b[商人] &rお前さんは既にそのレベルだぞ" to player

        else:
            play sound "block.note_block.banjo" to player
            message "&b[商人] &rレベルを下げることはできないな..." to player

    if event-item is iron pickaxe named "&lレベル2":
        cancel event
        if {level.%player%} is 1:
            if player has {price.levelup.to2} of emerald:
                set {level.%player%} to 2
                set {_cn} to {skill.%player%}
                give {Natto.weapon.%{_cn}%.2} to player
                execute console command "tag %player% add %{_cn}%-2"
                set player's max health to {Natto.maxHealth.%{_cn}%.2}
                heal player by 100 hearts
                remove {price.levelup.to2} of emerald from player
                play sound "entity.player.levelup" to player
                message "&b[商人] &rレベルをあげてやったぞ！" to player
                stop
            else:
                play sound "block.note_block.banjo" to player
                message "&b[商人] &rエメラルドが足りないようだな..." to player

        if {level.%player%} is 2:
            play sound "block.note_block.banjo" to player
            message "&b[商人] &rお前さんは既にそのレベルだぞ" to player

        if {level.%player%} is 3:
            play sound "block.note_block.banjo" to player
            message "&b[商人] &rレベルを下げることはできないな..." to player

    if event-item is diamond pickaxe named "&lレベル3":
        cancel event
        if {level.%player%} is 2:
            if player has {price.levelup.to3} of emerald:
                set {level.%player%} to 3
                set {_cn} to {skill.%player%}
                give {Natto.weapon.%{_cn}%.3} to player
                execute console command "tag %player% add %{_cn}%-3"
                set player's max health to {Natto.maxHealth.%{_cn}%.3}
                heal player by 100 hearts

                remove {price.levelup.to3} of emerald from player
                play sound "entity.player.levelup" to player
                message "&b[商人] &rレベルをあげてやったぞ！" to player
                stop
            else:
                play sound "block.note_block.banjo" to player
                message "&b[商人] &rエメラルドが足りないようだな..." to player

        if {level.%player%} is 3:
            play sound "block.note_block.banjo" to player
            message "&b[商人] &rお前さんは既にそのレベルだぞ" to player

        if {level.%player%} is 1:
            play sound "block.note_block.banjo" to player
            message "&b[商人] &rまずはレベル2になってくれ" to player
