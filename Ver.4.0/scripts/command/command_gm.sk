# ====================================================================================================================================================================================
#
# ===For Game Master===   OP所持者が実行可能
#
# ====================================================================================================================================================================================

# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# ゲームを開始します．
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /start:
    description:Start game.
    permission:op
    permission message:You Don't have permission.
    trigger:
        gameStart()



# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# ゲームを強制終了します．
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /end:
    description:End game.
    permission:op
    permission message:You Don't have permission.
    trigger:
        gameEnd()



# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# ジョインカウントを表示します．
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /joinCount:
    permission:op
    permission message:You Don't have permission.
    trigger:
        message "&2&l>JoinCount &r現在の接続人数は &6%online player count% &r人、累計接続人数は &6%{joinCount}% &r人です" to player



# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# admin．
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /admin [<text>] [<player>]:
    description:Toggle admin.
    permission:op
    permission message:You Don't have permission.
    trigger:
        if arg-1 is not set:
            if {admin.%player%} is true:
                adminLeave(player)
                set the player's chat name to "%name of player%"
                set the player's display name to "%name of player%"
                message "&2>SysLog-Admin &r%player% がadminから脱退しました" to {admin::*}
                message "&2>SysLog-Admin &radminから脱退しました" to player
                stop
            else:
                adminJoin(player)
                set the player's chat name to "&4%name of player%"
                set the player's display name to "&4[運営] %name of player%"
                message "&2>SysLog-Admin &r%player% がadminに加入しました" to {admin::*}
                stop

        if arg-1 is "join":
            if arg-2 is set:
                if {admin.%arg-2%} is false:
                    adminJoin(arg-2)
                    set the arg-2's chat name to "&4%name of arg-2%"
                    set the arg-2's display name to "&4[運営] %name of arg-2%"
                    message "&2>SysLog-Admin &r%player% があなたをadminに加入させました" to arg-2
                    message "&2>SysLog-Admin &r%arg-2% がadminに加入しました" to {admin::*}
                    stop
                else:
                    message "&2>SysLog-Admin &r%arg-2% はすでにadminです" to player
                    stop
            else:
                if {admin.%player%} is false:
                    adminJoin(player)
                    set the player's chat name to "&4%name of player%"
                    set the player's display name to "&4[運営] %name of player%"
                    message "&2>SysLog-Admin &r%player% がadminに加入しました" to {admin::*}
                    stop
                else:
                    message "&2>SysLog-Admin &r%player% はすでにadminです" to player
                    stop

        if arg-1 is "leave":
            if arg-2 is set:
                if {admin.%arg-2%} is true:
                    adminLeave(arg-2)
                    set the arg-2's chat name to "%name of arg-2%"
                    set the arg-2's display name to "%name of arg-2%"
                    message "&2>SysLog-Admin &r%player% があなたをadminから脱退させました" to arg-2
                    message "&2>SysLog-Admin &r%arg-2% がadminから脱退しました" to {admin::*}
                    stop
                else:
                    message "&2>SysLog-Admin &r%arg-2% はすでにadminではありません" to player
                    stop
            else:
                if {admin.%player%} is true:
                    adminLeave(player)
                    set the player's chat name to "%name of player%"
                    set the player's display name to "%name of player%"
                    message "&2>SysLog-Admin &r%player% がadminから脱退しました" to {admin::*}
                    message "&2>SysLog-Admin &radminから脱退しました" to player
                    stop
                else:
                    message "&2>SysLog-Admin &r%player% はすでにadminではありません" to player
                    stop



# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# プレイヤーをチームへ所属させます．一般プレイヤーのチーム選択時もこのコマンドがコンソール経由で実行されます.
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /join <text> [<player>]:
    description:Join team.
    permission:op
    permission message:You Don't have permission.
    trigger:
        loop {Natto.qua.Team} times:
            add 1 to {_loop}
            if arg-1 is {Natto.teamName::%{_loop}%}:
                if arg-2 is set:
                    teamJoin(arg-2, arg-1)
                else:
                    teamJoin(player, arg-1)
                stop
        message "&c&l>Error &r%arg-1%は存在しないチーム名です"




# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# admin権限でプレイヤーを復活させます。一般ユーザーによる復活はこのコマンドに依存しません。
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /revival:
    description:Revival Someone
    permission:op
    permission message:You Don't have permission.
    trigger:
        open chest inventory with 5 row named "&0復活させる人を選んでください" to player
        set slot 1 of player's current inventory to red wool named "&lRed Team"
        set slot 10 of player's current inventory to skull of {team.red::1} named "&l%{team.red::1}% &rを復活"
        set slot 19 of player's current inventory to skull of {team.red::2} named "&l%{team.red::2}% &rを復活"
        set slot 28 of player's current inventory to skull of {team.red::3} named "&l%{team.red::3}% &rを復活"
        set slot 37 of player's current inventory to red wool named "&lRed Team"

        set slot 2 of player's current inventory to blue wool named "&lBlue Team"
        set slot 11 of player's current inventory to skull of {team.blue::1} named "&l%{team.blue::1}% &rを復活"
        set slot 20 of player's current inventory to skull of {team.blue::2} named "&l%{team.blue::2}% &rを復活"
        set slot 29 of player's current inventory to skull of {team.blue::3} named "&l%{team.blue::3}% &rを復活"
        set slot 38 of player's current inventory to blue wool named "&lBlue Team"

        set slot 3 of player's current inventory to yellow wool named "&lYellow Team"
        set slot 12 of player's current inventory to skull of {team.yellow::1} named "&l%{team.yellow::1}% &rを復活"
        set slot 21 of player's current inventory to skull of {team.yellow::2} named "&l%{team.yellow::2}% &rを復活"
        set slot 30 of player's current inventory to skull of {team.yellow::3} named "&l%{team.yellow::3}% &rを復活"
        set slot 39 of player's current inventory to yellow wool named "&lYellow Team"

        set slot 4 of player's current inventory to green wool named "&lGreen Team"
        set slot 13 of player's current inventory to skull of {team.green::1} named "&l%{team.green::1}% &rを復活"
        set slot 22 of player's current inventory to skull of {team.green::2} named "&l%{team.green::2}% &rを復活"
        set slot 31 of player's current inventory to skull of {team.green::3} named "&l%{team.green::3}% &rを復活"
        set slot 40 of player's current inventory to green wool named "&lGreen Team"

        set slot 5 of player's current inventory to orange wool named "&lOrange Team"
        set slot 14 of player's current inventory to skull of {team.orange::1} named "&l%{team.orange::1}% &rを復活"
        set slot 23 of player's current inventory to skull of {team.orange::2} named "&l%{team.orange::2}% &rを復活"
        set slot 32 of player's current inventory to skull of {team.orange::3} named "&l%{team.orange::3}% &rを復活"
        set slot 41 of player's current inventory to orange wool named "&lOrange Team"

        set slot 6 of player's current inventory to purple wool named "&lPurple Team"
        set slot 15 of player's current inventory to skull of {team.purple::1} named "&l%{team.purple::1}% &rを復活"
        set slot 24 of player's current inventory to skull of {team.purple::2} named "&l%{team.purple::2}% &rを復活"
        set slot 33 of player's current inventory to skull of {team.purple::3} named "&l%{team.purple::3}% &rを復活"
        set slot 42 of player's current inventory to purple wool named "&lPurple Team"

        set slot 7 of player's current inventory to black wool named "&lblack Team"
        set slot 16 of player's current inventory to skull of {team.black::1} named "&l%{team.black::1}% &rを復活"
        set slot 25 of player's current inventory to skull of {team.black::2} named "&l%{team.black::2}% &rを復活"
        set slot 34 of player's current inventory to skull of {team.black::3} named "&l%{team.black::3}% &rを復活"
        set slot 43 of player's current inventory to black wool named "&lblack Team"



# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# 掘削の可否を設定します
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /destroyable [<text>]:
    description:Define destroyable
    permission:op
    permission message:You Don't have permission.
    aliases:/des
    trigger:
        if arg-1 is "true":
            set {destroyable} to "true"
            broadcast "&b[運営] &rブロックの破壊が可能になりました"
        if arg-1 is "false":
            set {destroyable} to "false"
            broadcast "&b[運営] &rブロックの破壊が不可能になりました"
        if arg-1 is not set:
            if {destroyable} is "false":
                set {destroyable} to "true"
                broadcast "&b[運営] &rブロックの破壊が可能になりました"
                stop
            if {destroyable} is "true":
                set {destroyable} to "false"
                broadcast "&b[運営] &rブロックの破壊が不可能になりました"



# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# 安地の範囲を設定しなおします．これはゲーム開始後に設定する場合のコマンドです．*速度の設定は必須です
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /anntirange <number> <number>:
    description:Define SafeZone range and speed
    permission:op
    permission message:You Don't have permission.
    usage:/anntirange <範囲> <速度> ※速度の値は最終安地までの残り時間に同じ
    trigger:
        execute console command "worldborder set %arg-1%"
        broadcast "&b&l>Game &r安地のサイズを%arg-1%に変更しました"
        execute console command "worldborder set 10 %arg-2%"
        broadcast "&b&l>Game &r収縮完了までの時間を%arg-2%に変更しました"



# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# 安地の収縮スピードのみを設定しなおします．範囲は現在の状態のまま進行します．
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /anntispeed <number>:
    description:Define SafeZone speed
    permission:op
    permission message:You Don't have permission.
    usage:/speed <速度> ※速度の値は最終安地までの残り時間に同じ
    trigger:
        execute console command "worldborder set 10 %arg-1%"
        broadcast "&b&l>Game &r収縮完了までの時間を%arg-1%に変更しました"



# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# 本を渡します．
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /book:
    description:Give ibuibuBook
    permission:op
    permission message:You Don't have permission.
    trigger:
        make player execute command "function shimashima:02_admin-book"



# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# 村人のスポーンエッグを渡します．
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /murabito:
    description:Give Villager's SpawnEgg
    permission:op
    permission message:You Don't have permission.
    trigger:
        make player execute command "function shimashima:02_murabito"



# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# エメラルドを渡します.
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /emerald <number>:
    description:Give Emeralds
    permission:op
    permission message:You Don't have permission.
    trigger:
        give arg-1 of emerald to player



# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# Mode Change
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /mode <text> [<text>]:
    description:Switch Mode
    permission:op
    permission message:You Don't have permission.
    trigger:


        if arg-1 is "team":
            if arg-2 is "on":
                set {mode.team} to true
                broadcast "&b&l>Game &r「チーム戦」が&aオン&rになりました"
                stop
            if arg-2 is "off":
                set {mode.team} to false
                broadcast "&b&l>Game &r「チーム戦」が&cオフ&rになりました"
                stop
            else:
                if {mode.team} is true:
                    execute console command "mode team off"
                    stop
                else:
                    execute console command "mode team on"
                    stop

        if arg-1 is "skill":
            message "&c&l>Error &r廃止しました" to player

        if arg-1 is "daruma":
            if arg-2 is "on":
                set {mode.daruma} to true
                broadcast "&b&l>Game &r「だるまさんが転んだモード」が&aオン&rになりました"
                stop
            if arg-2 is "off":
                set {mode.daruma} to false
                broadcast "&b&l>Game &r「だるまさんが転んだモード」が&cオフ&rになりました"
                stop
            else:
                if {mode.daruma} is true:
                    execute console command "mode daruma off"
                    stop
                else:
                    execute console command "mode daruma on"
                    stop

        if arg-1 is "yumi":
            if arg-2 is "on":
                set {mode.yumi} to true
                broadcast "&b&l>Game &r「弓のみモード」が&aオン&rになりました"
                stop
            if arg-2 is "off":
                set {mode.yumi} to false
                broadcast "&b&l>Game &r「弓のみモード」が&cオフ&rになりました"
                stop
            else:
                if {mode.yumi} is true:
                    execute console command "mode yumi off"
                    stop
                else:
                    execute console command "mode yumi on"
                    stop

        if arg-1 is "yukidama":
            if arg-2 is "on":
                set {mode.yukidama} to true
                broadcast "&b&l>Game &r「雪玉のみモード」が&aオン&rになりました"
                stop
            if arg-2 is "off":
                set {mode.yukidama} to false
                broadcast "&b&l>Game &r「雪玉のみモード」が&cオフ&rになりました"
                stop
            else:
                if {mode.yukidama} is true:
                    execute console command "mode yukidama off"
                    stop
                else:
                    execute console command "mode yukidama on"
                    stop

        if arg-1 is "ibuibu":
            if {mode.ibuibu} is "true":
                set {mode.ibuibu} to "false"
                execute console command "scoreboard players set ibuibumode ibuibumode 0"
                broadcast "&b[運営] &r「ibuibu Mode」がオフになりました"
            else:
                set {mode.ibuibu} to "true"
                execute console command "scoreboard players set ibuibumode ibuibumode 1"
                broadcast "&b[運営] &r「ibuibu Mode」がオンになりました"



# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# lobby tp
# ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /lobby:
    description:Teleport player to lobby
    permission:op
    permission message:You Don't have permission.
    trigger:
        teleport player to {Natto.locaLobbySpawn}