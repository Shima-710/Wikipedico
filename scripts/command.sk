# ===============================================================================================================================================================================================
# 一般コマンド
# 通常利用で実行可能性のあるコマンド群
# ===============================================================================================================================================================================================            


# ===============================================================================================================================================================================================
# ===For User===   全ユーザーが実行可能
# ===============================================================================================================================================================================================
# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# サイドバーのリセットをします．ロビーでのみ有効です．
# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /sidebar:
    description:Wipe sidebar
    trigger:
        if {status} is 0:
            set {sidebar.%player%} to 0



# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# バージョン情報を提示します
# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /ver:
    description:Show version now working
    trigger:
        message "&r現在のバージョンは &c%{ver}% &rです" to player





# ===============================================================================================================================================================================================
# ===For Game Master===   OP保持者もしくはadmin経験者(LP権限グループ"Skript.admin")のみ実行可能
# ===============================================================================================================================================================================================
# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# ゲームを開始，または強制終了します．
# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /game <text>:
    description:Start or end the game
    permission: skript.admin
    permission message:Admin専用コマンドです。
    usage:/game <start/end/reset>
    trigger:
        if arg-1 is "start":
            loop all players:
                set {sidebar.%loop-player%} to 1
            set {qua.player} to online player count
            reduce {qua.player} by {qua.admin}
            set {surviver} to {qua.player}
            set {rank.dummy} to {qua.player}
            set {rank.alive} to "--"
            set {status} to 1
            broadcast "&b[運営] &rゲームを開始します..."
            wait 1 seconds
            execute console command "function shimashima:cd_3"
            loop all players:
                play sound "block.note_block.harp" to loop-player
            wait 1 seconds
            execute console command "function shimashima:cd_2"
            loop all players:
                play sound "block.note_block.harp" to loop-player
            wait 1 seconds
            execute console command "function shimashima:cd_1"
            loop all players:
                play sound "block.note_block.harp" to loop-player
            wait 1 seconds
            execute console command "function shimashima:01_game_start"
            loop all players:
                play sound "entity.enderman.teleport" to loop-player
            stop
        if arg-1 is "end":
            execute console command "function shimashima:01_game_end"
            loop all players:
                play sound "ui.toast.challenge_complete" with volume 0.8 to loop-player



# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# adminに入ったり出たりします．
# adminは変数{admin.%player%}またはチーム"admin"で判別
# 一度でもadminになればLP権限グループ"skript.admin"に追加され，OP出なくともコマンドの実行は可能になります
# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /admin [<text>] [<player>]:
    description:Make specific user to admin
    permission: skript.admin
    permission message:Admin専用コマンドです。
    usage:/admin <join/leave> [<プレイヤー>] ※プレイヤー未指定の場合は自身を操作します
    aliases:/ad
    trigger:
        if arg-1 is "join":
            if arg-2 is set:
                if {admin.%arg-2%} is "true":
                    message "&b[admin] &r%arg-2%はすでにadminです" to player
                else:
                    execute console command "op %arg-2%"
                    make player execute command "/gamemode creative %arg-2%"
                    make player execute command "/team join admin %arg-2%"
                    execute console command "lp user %arg-2% parent add skript.admin"
                    message "&b[admin] &r%arg-2%をAdminに追加しました。" to player
                    message "&b[admin] &r%player%があなたをAdminに追加しました。" to arg-2
                    set {admin.%arg-2%} to "true"
                    set the arg-2's tab name to "&c[運営] &r%name of arg-2%"
                    set the arg-2's chat name to "&c%name of arg-2%"
                    set the arg-2's display name to "&c%name of arg-2%"
                    add 1 to {qua.admin}
            if arg-2 is not set:
                if {admin.%player%} is "true":
                    message "&b[admin] &rあなたはすでにadminです" to player
                else:
                    execute console command "op %player%"
                    make player execute command "/gamemode creative %player%"
                    make player execute command "/team join admin %player%"
                    execute console command "lp user %player% parent add skript.admin"
                    message "&b[admin] &r%player%をAdminに追加しました。" to player
                    set {admin.%player%} to "true"
                    set the player's tab name to "&c[運営] &r%name of player%"
                    set the player's chat name to "&c%name of player%"
                    set the player's display name to "&c%name of player%"
                    add 1 to {qua.admin}
        if arg-1 is "leave":
            if arg-2 is set:
                if {admin.%arg-2%} is "false":
                    message "&b[admin] &r%arg-2%はすでにadminではありません" to player
                else:
                    make player execute command "/gamemode adventure %arg-2%"
                    make player execute command "/effect clear %arg-2% minecraft:night_vision"
                    make player execute command "/team leave %arg-2%"
                    message "&b[admin] &r%arg-2%をAdminから除外しました。" to player
                    message "&b[admin] &r%player%があなたをAdminから除外しました。" to arg-2
                    set {admin.%arg-2%} to "false"
                    set the arg-2's tab name to "%name of arg-2%"
                    set the arg-2's chat name to "%name of arg-2%"
                    set the arg-2's display name to "%name of arg-2%"
                    reduce {qua.admin} by 1
            if arg-2 is not set:
                if {admin.%player%} is "false":
                    message "&b[admin] &rあなたはすでにadminではありません" to player
                else:
                    make player execute command "/gamemode adventure %player%"
                    make player execute command "/effect clear @s minecraft:night_vision"
                    make player execute command "/team leave %player%"
                    message "&b[admin] &r%player%をAdminから除外しました。" to player
                    set {admin.%player%} to "false"
                    set the player's tab name to "%name of player%"
                    set the player's chat name to "%name of player%"
                    set the player's display name to "%name of player%"
                    reduce {qua.admin} by 1
        if arg-1 is not set:
            if {admin.%player%} is "true":
                make player execute command "/gamemode adventure %player%"
                make player execute command "/effect clear @s minecraft:night_vision"
                make player execute command "/team leave %player%"
                message "&b[admin] &r%player%をAdminから除外しました。" to player
                set {admin.%player%} to "false"
                set the player's tab name to "%name of player%"
                set the player's chat name to "%name of player%"
                set the player's display name to "%name of player%"
                reduce {qua.admin} by 1
                stop
            if {admin.%player%} is "false":
                execute console command "op %player%"
                make player execute command "/gamemode creative %player%"
                make player execute command "/team join admin %player%"
                execute console command "lp user %player% parent add skript.admin"
                message "&b[admin] &r%player%をAdminに追加しました。" to player
                set {admin.%player%} to "true"
                set the player's tab name to "&c[運営] &r%name of player%"
                set the player's chat name to "&c%name of player%"
                set the player's display name to "&c%name of player%"
                add 1 to {qua.admin}



# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# 掘削の可否を設定します
# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /destroyable <text>:
    description:Define destroyable
    permission: skript.admin
    permission message:Admin専用コマンドです。
    trigger:
        if arg-1 is "true":
            set {destroyable} to "true"
            broadcast "&b[運営] &rブロックの破壊が可能になりました"
        if arg-1 is "false":
            set {destroyable} to "false"
            broadcast "&b[運営] &rブロックの破壊が不可能になりました"



# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# 安地の範囲を設定しなおします．これはゲーム開始後に設定する場合のコマンドです．
# 速度の設定は必須です
# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /anntirange <number> <number>:
    description:Define SafeZone range and speed
    permission: skript.admin
    permission message:Admin専用コマンドです。
    usage:/anntirange <範囲> <速度> ※速度の値は最終安地までの残り時間に同じ
    trigger:
        execute console command "worldborder set %arg-1%"
        broadcast "&b[運営] &r安地のサイズを%arg-1%に変更しました"
        execute console command "worldborder set 10 %arg-2%"
        broadcast "&b[運営] &r収縮完了までの時間を%arg-2%に変更しました"
        


# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# 安地の収縮スピードのみを設定しなおします．範囲は現在の状態のまま進行します．
# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /anntispeed <number>:
    description:Define SafeZone speed
    permission: skript.admin
    permission message:Admin専用コマンドです。
    usage:/speed <速度> ※速度の値は最終安地までの残り時間に同じ
    trigger:
        execute console command "worldborder set 10 %arg-1%"
        broadcast "&b[運営] &r収縮完了までの時間を%arg-1%に変更しました"



# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# 本を渡します．
# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /book:
    description:Give ibuibuBook
    permission: skript.admin
    permission message:Admin専用コマンドです。
    trigger:
        make player execute command "function shimashima:02_admin-book"



# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# 村人のスポーンエッグを渡します．
# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /murabito:
    description:Give Villager's SpawnEgg
    permission: skript.admin
    permission message:Admin専用コマンドです。
    trigger:
        make player execute command "function shimashima:02_murabito"



# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# エメラルドを渡します.
# ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
command /emerald <number>:
    description:Give Emeralds
    permission: skript.admin
    permission message:Admin専用コマンドです。
    trigger:
        give arg-1 of emerald to player