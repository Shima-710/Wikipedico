function resetTeamVariables():

    loop {Natto.qua.Team} times:
        add 1 to {_loop}
        set {_c} to {Natto.teamName::%{_loop}%}
        delete {team.%{_c}%::*}
        delete {admin::*}
        set {qua.surviveTeam.%{_c}%} to 0
        set {teamStatus.%{_c}%} to "death"
        set {qua.player.team.%{_c}%} to 0
        set {qua.surviver.team.%{_c}%} to 0
        set {teamRank.%{_loop}%} to 0

function resetPlayerVariables(p: player):
    set {playerJoined.%{_p}%} to false
    set {playerRank.%{_p}%} to 0
    set {kills.%{_p}%} to 0
    set {playerStatus.%{_p}%} to "alive"
    set {admin.%{_p}%} to false
    set {spectator.%{_p}%} to false
    set {team.openingchest.%{_p}%} to false
    set {team.openingchest2.%{_p}%} to false

function resetRankingVariables():
    loop {qua.player} times:
        add 1 to {_loop}
        clear {soloRanking.name.%{_loop}%}
        clear {soloRanking.kills.%{_loop}%}
    delete {killRanking::*}
    delete {killRanking.sorted::*}

function resetGameVariables():
    set {gameStatus} to 0
    set {qua.playTeam} to 0
    set {qua.surviveTeam} to 0
    set {qua.player} to 0
    set {qua.surviver} to 0
    set {qua.admin} to 0
    set {qua.spectator} to 0
    set {destroyable} to false
    delete {death::*}
    set {endingTime} to false

function resetGameMode():
    set {mode.team} to {Natto.defaultModeTeam}
    set {mode.2team} to {Natto.defaultMode2Team}
    set {mode.daruma} to {Natto.defaultModeDaruma}
    set {mode.yuki} to {Natto.defaultModeYuki}
    set {mode.ibuibu} to {Natto.defaultModeIbuibu}


function gameStart():
    if {defineloca} is true:
        broadcast "&c&l>Error &r現在デバッグモードです.リロードして終了してください."
        stop
    if {mode.team} is true:
        loop all players:
            if {playerJoined.%loop-player%} is false:
                broadcast "&c&l>Error &rチームに所属してないプレイヤーがいるためゲームを開始できませんでした"
                stop
        set {_loop} to 1
        loop {Natto.qua.Team} times:
            applyTeam({Natto.teamName::%{_loop}%})
            add 1 to {_loop}

        setQuas()


    else:
        setQuas()
        execute console command "team join players @a[team=!admin]"


    set {gameStatus} to 1
    broadcast "&b&l>Game &r%{Natto.prepareTime}%秒後にゲームを開始します..."
    teleport all players to {Natto.locaSky}
    execute console command "scoreboard objectives setdisplay sidebar"
    execute console command "scoreboard players set on_game on_game 1"
    execute console command "function shimashima:01_goodbye_item"
    execute console command "function shimashima:01_toggle_nametag"
    execute console command "effect give @a[team=!admin] glowing %{Natto.prepareTime}% 255 true"
    execute console command "kill @e[type=item]"
    if {mode.ibuibu} is true:
        execute console command "replaceitem entity @a[team=!admin] armor.head minecraft:player_head{SkullOwner:iibukuro}"
    set {_loop} to {Natto.prepareTime}
    loop {Natto.prepareTime} times:
        loop all players:
            set loop-player's level to {_loop}
        if {_loop} is {Natto.countdownTimes}:
            send title "&6&l%{_loop}%" to all players
            play sound "block.note_block.harp" to all players
            reduce {Natto.countdownTimes} by 1
        if {_loop} is 1:
            wait 1 second
            execute console command "function shimashima:01_game_start"
            execute console command "kill @e[type=item]"
            send title "&e&lSTART" to all players
            play sound "entity.enderman.teleport" to all players
            loop all players:
                set loop-player's level to 0
                teleport loop-player to location 5 blocks below the loop-player
        reduce {_loop} by 1
        wait 1 second


    if {mode.daruma} is true:
        set {daruma.count} to true



function gameEnd():
    set {endingTime} to true
    play sound "ui.toast.challenge_complete" with volume 0.8 to all players
    execute console command "function shimashima:01_game_end"
    execute console command "spawnpoint @a %{Natto.locaLobbySpawn}%"
    execute console command "kill @e[type=item]"

    loop {Natto.qua.Team} times:
        add 1 to {_loop}
        execute console command "scoreboard players reset 残り人数：%{qua.surviver}%人 %{Natto.teamName::%{_loop}%}%"
        execute console command "scoreboard players reset 残りチーム数：%{qua.surviveTeam}%チーム %{Natto.teamName::%{_loop}%}%"

    set {gameStatus} to 0

    if {mode.daruma} is true:
        set {daruma.count} to false

    wait 5 seconds
    broadcast "&b&l>Game &r5秒後にロビーへ戻ります..."
    execute console command "kill @e[type=item]"
    wait 5 seconds

    execute console command "function shimashima:01_toggle_nametag"
    remove all items from all players
    execute console command "kill @e[type=item]"
    execute console command "clear @a"
    set gamemode of all players to adventure
    spawnChest()
    teleport all players to {Natto.locaLobbySpawn}

    wait 5 seconds
    broadcast "&b&l>Game &r試合結果を集計しています..."
    wait 3 seconds
    if {mode.team} is true:
        calTeamRanking()
    else:
        calSoloRanking()
    wait 4 seconds
    broadcast "&b&l>Game &rキル数ランキングを集計しています..."
    wait 3 seconds
    calKillRanking()

    resetRankingVariables()
    loop all players:
        if {admin.%loop-player%} is true:
            adminLeave(loop-player)
        if {playerStatus.%{_p}%} is "spectator":
            spcLeave(loop-player)
    wait 3 second
    reloadPlugin()


function winTeam():
    loop {Natto.qua.Team} times:
        add 1 to {_loop}
        set {_cc} to {Natto.teamName::%{_loop}%}
        if {teamStatus.%{_cc}%} is "alive":
            set {_c} to {_cc}
    set {teamStatus.%{_c}%} to "death"
    set {teamRank.1} to {_c}
    execute console command "scoreboard players set 順位：1位 %{_c}% -3"
    set {_dispc} to low2up({_c})
    send title "&e&l勝利 &8| &r[%{_dispc}% &r&lTeam&r]" to all players
    gameEnd()

function winPlayer():
    loop all players:
        if loop-player's gamemode is survival:
            set {_p} to loop-player
    set {playerStatus.%{_p}%} to "death"
    set {playerRank.%{_p}%} to 1
    send title "&e&l勝利 &8| &6%{_p}%" to all players
    gameEnd()



function adminJoin(p: player):
    teamLeave({_p})
    set gamemode of {_p} to creative
    make {_p} execute command "/team join admin %{_p}%"
    make {_p} execute command "/book"
    set {admin.%{_p}%} to true
    add {_p} to {admin::*}
    add 1 to {qua.admin}
    set {playerJoined.%{_p}%} to "admin"
    set {playerStatus.%{_p}%} to "admin"

function adminLeave(p: player):
    teamLeave({_p})
    set gamemode of {_p} to adventure
    make {_p} execute command "/team leave %{_p}%"
    make {_p} execute command "/effect clear %{_p}% minecraft:night_vision"
    set {admin.%{_p}%} to false
    remove {_p} from {admin::*}
    reduce {qua.admin} by 1
    set {playerJoined.%{_p}%} to false
    set {playerStatus.%{_p}%} to "alive"


function spcJoin(p: player):
    teamLeave({_p})
    set gamemode of {_p} to spectator
    make {_p} execute command "/team join admin %{_p}%"
    add {_p} to {spectator::*}
    add 1 to {qua.spectator}
    set {playerJoined.%{_p}%} to "spectator"
    set {playerStatus.%{_p}%} to "spectator"

function spcLeave(p: player):
    teamLeave({_p})
    set gamemode of {_p} to adventure
    make {_p} execute command "/team leave %{_p}%"
    make {_p} execute command "/effect clear %{_p}% minecraft:night_vision"
    remove {_p} from {spectator::*}
    reduce {qua.spectator} by 1
    set {playerJoined.%{_p}%} to false
    set {playerStatus.%{_p}%} to "spectator"


function teamJoin(p: player, c: text):
    if {qua.player.team.%{_c}%} is larger than {Natto.teamMaxPlayer}:
        message "&c&l>Error すでに満員です" to {_p}
        stop
    else:
        if {playerStatus.%{_p}%} is "admin":
            adminLeave({_p})
        if {playerStatus.%{_p}%} is "spectator":
            spcLeave({_p})
        teamLeave({_p})
        add {_p} to {team.%{_c}%::*}
        add 1 to {qua.player.team.%{_c}%}
        add 1 to {qua.surviver.team.%{_c}%}
        set {playerJoined.%{_p}%} to {_c}
        execute console command "scoreboard players set %name of {_p}% %{_c}% 1"
        execute console command "team join %{_c}% %{_p}%"
        play sound "entity.player.levelup" to {_p}
        set {_dispc} to low2up({_c})
        message "&b&l>System &r%{_dispc}% チームに入りました！" to {_p}

function teamLeave(p: player):  #{_oc}は抜ける前に入ってたチームカラー(old color)
    if {playerJoined.%{_p}%} is false:
        stop
    else if {playerJoined.%{_p}%} is not set:
        stop
    else:
        set {_oc} to {playerJoined.%{_p}%}
        remove {_p} from {team.%{_oc}%::*}
        reduce {qua.player.team.%{_oc}%} by 1
        reduce {qua.surviver.team.%{_oc}%} by 1
        set {playerJoined.%{_p}%} to false
        execute console command "scoreboard players reset %name of {_p}% %{_oc}%"
        execute console command "team leave %{_p}%"

function applyTeam(c: object):
    if amount of {team.%{_c}%::*} is not 0:
        add 1 to {qua.playTeam}
        add 1 to {qua.surviveTeam}
        set {teamStatus.%{_c}%} to "alive"
    else:
        set {teamStatus.%{_c}%} to "death"

function teamChat(p:player, t: text):
    if {playerJoined.%{_p}%} is false:
        stop
    set {_c} to {playerJoined.%{_p}%}
    message "&7[Team Chat] <&r%{_p}% &7-> &r%{_c}%&7> &r%{_t}%" to {team.%{_c}%::*} and {admin::*} and {spectator::*}


#FIXME:okasii
function calTeamRanking(): # %{teamrank.%{_loop}%}%の中身は色
    loop all players:
        add {kills.%loop-player%} to {teamKills.%{playerJoined.%loop-player%}%}
    broadcast "&7====================================================="
    broadcast "&b&l[試合結果]"
    set {_loop} to 1
    set {teamRankLoop} to true
    while {teamRankLoop} is true:
        set {_dispc} to low2up({teamRank.%{_loop}%})
        broadcast "&6%{_loop}%位  &8-- &r[%{_dispc}%] : %{team.%{teamRank.%{_loop}%}%::*}% &8-- &6計%{teamKills.%{teamRank.%{_loop}%}%}%kill"
        if {_loop} is {qua.playTeam}:
            set {teamRankLoop} to false
        add 1 to {_loop}
    broadcast "&7====================================================="

function calSoloRanking():
    loop all players:
        set {soloRanking.name.%{playerRank.%loop-player%}%} to name of loop-player
        set {soloRanking.kills.%{playerRank.%loop-player%}%} to {kills.%loop-player%}
    broadcast "&7====================================================="
    broadcast "&b&l[試合結果]"
    loop {qua.player} times:
        add 1 to {_loop}
        broadcast "&6%{_loop}%位  &8--&r%{soloRanking.name.%{_loop}%}% &8-- &6計%{soloRanking.kills.%{_loop}%}%kill"
    broadcast "&7====================================================="

#FIXME:チーム戦でバグり？
function calKillRanking():
    loop all players:
        add "&6%{kills.%loop-player%}%kill &8-- &6%name of loop-player%" to {killRanking::*}
    set {killRanking.sorted::*} to sorted {killRanking::*}
    broadcast "&7====================================================="
    broadcast "&b&l[個人別キルランキング] &r<上位%{Natto.KillRankingTimes}%名>"
    set {_loop.down} to amount of {killRanking.sorted::*}
    set {_loop.up} to 1
    loop {Natto.KillRankingTimes} times:
        broadcast "&6%{_loop.up}%位 &8-- %{killranking.sorted::%{_loop.down}%}%"
        reduce {_loop.down} by 1
        add 1 to {_loop.up}
    broadcast "&7====================================================="


function deathPlayer(p: player):
    set {playerStatus.%{_p}%} to "death"
    add {_p} to {death::*}
    set gamemode of {_p} to spectator
    execute console command "spawnpoint %{_p}% %{Natto.locaDeathSpawn}%"
    if {mode.team} is true:
        if {playerJoined.%{_p}%} is false:
            stop
        set {_c} to {playerJoined.%{_p}%}
        reduce {qua.surviver.team.%{_c}%} by 1
        if {qua.surviver.team.%{_c}%} is 0:
            deathTeam("%{_c}%")
        reduce {qua.surviver} by 1
    else:
        set {playerRank.%{_p}%} to {qua.surviver}
        reduce {qua.surviver} by 1
        if {qua.surviver} is 1:
            winPlayer()

function deathTeam(c: text):
    set {teamStatus.%{_c}%} to "death"
    set {teamRank.%{qua.surviveTeam}%} to {_c}
    reduce {qua.surviveTeam} by 1
    wait 1 tick
    set {_dispc} to low2up({_c})
    broadcast "&b&l>Game &r%{_dispc}% チームが全滅しました"
    if {qua.surviveTeam} is 1:
        winTeam()


function low2up(c: text) :: text:
    if {_c} is "red":
        set {_rt} to "&c&lRed&r"
    else if {_c} is "blue":
        set {_rt} to "&9&lBlue&r"
    else if {_c} is "yellow":
        set {_rt} to "&e&lYellow&r"
    else if {_c} is "green":
        set {_rt} to "&a&lGreen&r"
    else if {_c} is "orange":
        set {_rt} to "&6&lOrange&r"
    else if {_c} is "purple":
        set {_rt} to "&d&lPurple&r"
    else if {_c} is "black":
        set {_rt} to "&0&lBlack&r"

    return {_rt}


function revivePlayer(p: player, pp: player): # p:復活する本人 pp:復活させる人
    if {playerJoined.%{_p}%} is false:
        message "&2>SysLog-revivePlayer &rError occured." to {admin::*}
        message "CODE-T02 : {_p}=%{_p}%" to {admin::*}
        message "&c&l>Error &rそのプレイヤーは復活させられません" to {_pp}
        stop
    set {_c} to {playerJoined.%{_p}%}
    if {playerStatus.%{_p}%} is "death":
        if {mode.team} is true:
            if {teamStatus.%{_c}%} is "death":
                message "&c&l>Error &rすでに全滅したチームのプレイヤーは復活させられません" to {_pp}
                stop
            else:
                add 1 to {qua.surviver.team.%{_c}%}
        set {playerStatus.%{_p}%} to "alive"
        add 1 to {qua.surviver}
        reset {playerRank.%{_p}%}
        set gamemode of {_p} to survival
        teleport {_p} to {_pp}
        play sound "entity.player.levelup" to {_p} and {_pp}
    else:
        message "&c&l>Error &rそのプレイヤーは復活させられません" to {_pp}

function selectRevivePlayer(pp: player): # p:復活する人 pp:復活させる人
    if {playerJoined.%{_pp}%} is false:
        message "&2>SysLog-selectRevivePlayer &rError occured." to {admin::*}
        message "CODE-T03 : {_pp}=%{_pp}%" to {admin::*}
        message "&c&l>Error &rそのプレイヤーは復活させられません" to {_pp}
        stop
    set {_c} to {playerJoined.%{_pp}%}
    set {_loop} to 1
    set {_loop2} to 3
    loop {Natto.teamMaxPlayer} times:
        set slot {_loop2} of current inventory of {_pp} to skull of {team.%{_c}%::%{_loop}%} named "&l%{team.%{_c}%::%{_loop}%}% &rを復活"
        add 1 to {_loop}
        add 1 to {_loop2}

function setQuas():
    set {qua.online} to online player count
    set {qua.player} to {qua.online}
    reduce {qua.player} by {qua.admin}
    reduce {qua.player} by {qua.spectator}
    set {qua.surviver} to {qua.player}

function debugMode(b: boolean):
    if {_b} is true:
        execute console command "gamerule sendCommandFeedback true"
        execute console command "gamerule reducedDebugInfo false"

function spawnChest():
    loop {Natto.qua.locaChest} times:
        add 1 to {_loop}
        set block at {Natto.locaChest::%{_loop}%} to air
        wait 1 tick
        set block at {Natto.locaChest::%{_loop}%} to chest
        set {_chest} to block at {Natto.locaChest::%{_loop}%}
        set {_random.chestSlotQua} to random integer between 1 and {Natto.maxChestSlotQua}
        loop {_random.chestSlotQua} times:
            set {_random.chestSlotNum} to random integer between 0 and 26
            set {_random.itemNumber} to random integer between 1 and {Natto.qua.ChestItem.normal}
            add {Natto.chestRoot.normal::%{_random.itemNumber}%} to {_chest}'s inventory

function defChestRootNormal(n: number):
    loop {Natto.chestItemChance.normal::%{_n}%} times:
        add {Natto.chestItemName.normal::%{_n}%} to {Natto.chestRoot.normal::*}

function reloadConfig():
    delete {Natto.teamName::*}
    delete {Natto.chestItemName.normal::*}
    delete {Natto.chestItemChance.normal::*}
    delete {Natto.chestRoot.normal::*}
    gameConfig()
    add "red" to {Natto.teamName::*}
    add "blue" to {Natto.teamName::*}
    add "yellow" to {Natto.teamName::*}
    add "green" to {Natto.teamName::*}
    add "orange" to {Natto.teamName::*}
    add "purple" to {Natto.teamName::*}
    add "black" to {Natto.teamName::*}
    set {Natto.qua.Team} to amount of {Natto.teamName::*}
    set {Natto.qua.locaChest} to amount of {Natto.locaChest::*}
    set {Natto.qua.ChestItemName.normal} to amount of {Natto.chestItemName.normal::*}
    set {_loop1} to 0
    loop {Natto.qua.ChestItemName.normal} times:
        add 1 to {_loop1}
        defChestRootNormal({_loop1})
    set {Natto.qua.ChestItem.normal} to amount of {Natto.chestRoot.normal::*}

function reloadPlugin():
    set {defineloca} to false
    wait 2 tick
    reloadConfig()
    wait 2 tick
    resetTeamVariables()
    wait 2 tick
    resetRankingVariables()
    wait 2 tick
    resetGameVariables()
    wait 2 tick
    resetGameMode()
    wait 2 tick
    spawnChest()
    wait 2 tick
    debugMode({debugMode})
    wait 2 tick
    wait 2 tick
    loop all players:
        resetPlayerVariables(loop-player)
        teamLeave(loop-player)
        if {admin.%loop-player%} is true:
            adminLeave(loop-player)

    wait 1 tick
    execute console command "natto---teamgoodbye"
    wait 1 tick
    execute console command "function shimashima:00_load"
    wait 1 tick
    execute console command "experience set @a 0 points"
    wait 1 tick
    execute console command "experience set @a 0 levels"
    wait 1 tick
    loop {Natto.qua.Team} times:
        add 1 to {_loop}
        set {_c} to {Natto.teamName::%{_loop}%}
        execute console command "scoreboard players set ============== %{_c}% 0"
    execute console command "worldborder set 50000"
    execute console command "kill @e[type=item]"

    wait 3 tick
    loop all players:
        make loop-player execute command "rirogu"

